version = '0.1-SNAPSHOT'

apply plugin: "sonar-runner"

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'jacoco'
    
    group = 'com.daeliin.framework'

    ext {
        springBootVersion = '1.2.5.RELEASE'
        groovyVersion = '2.4.3'
        logbackVersion = '1.1.3'
        commonsLangVersion = '3.4'
        lombockVersion = '1.16.4'
        h2databaseVersion= '1.4.187'
    }
    
    buildscript {
        repositories {
            mavenCentral()
        }

        dependencies {
            classpath("org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion")
        }
    }
    
    configurations {
        provided
    }
     
    sourceSets {
        main.compileClasspath += configurations.provided
        test.compileClasspath += configurations.provided
        test.runtimeClasspath += configurations.provided
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    dependencies {
        // Groovy
        compile "org.codehaus.groovy:groovy-all:$groovyVersion"
    
        // Logback
        compile "ch.qos.logback:logback-classic:$logbackVersion"
        
        // Apache commons
        compile "org.apache.commons:commons-lang3:$commonsLangVersion"
        
        // Lombock
        provided "org.projectlombok:lombok:$lombockVersion"
    }
    
    test {
        useTestNG() 
        
        testLogging {
            showStackTraces true
            showExceptions true
            showCauses true
            events "passed", "skipped", "failed"
            afterSuite { desc, result ->
                if (!desc.parent) { 
                  println "\n${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)\n"
                }
            }
        }
    }
    
    uploadArchives {
        repositories {
            mavenDeployer {
                pom {
                    project {
                        parent {
                            groupId "org.springframework.boot"
                            artifactId "spring-boot-starter-parent"
                            version "1.2.5.RELEASE"
                        }
                    }
                }
            }
        }
    }
}

sonarRunner {
    sonarProperties {
        property "sonar.host.url", "$sonarHostUrl"
        property "sonar.jdbc.url", "$sonarJdbcUrl" 
        property "sonar.jdbc.driverClassName", "$sonarJdbcDriver"
        property "sonar.jdbc.username", "$sonarJdbcUsername"
        property "sonar.jdbc.password", "$sonarJdbcPassword"
        property "sonar.login", "$sonarLogin"
        property "sonar.password", "$sonarPassword"
    }
}

allprojects {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    
    task wrapper(type: Wrapper) { 
        gradleVersion = '2.5' 
    }
}